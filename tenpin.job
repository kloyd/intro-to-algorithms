// FOR
*LIST SOURCE PROGRAM
*IOCS(1132 PRINTER,CARD,DISK)
*ONE WORD INTEGERS
C BOWLING - COMPUTE 10 PIN BOWLING SCORES
C  
C EACH CARD HAS THE NUMBER OF PINS KNOCKED DOWN BY ONE BALL.
C   

C PINS 1 2 AND 3 FOR EACH LOOP THROUGH A FRAME.
      INTEGER P1, P2, P3
C FRAME NUMBER 
      INTEGER FN
C FRAME TOTAL
      INTEGER FT
C GRAND TOTAL
      INTEGER GT
C PRINT PROGRAM TITLE
      WRITE(3,5)
    5 FORMAT('1BOWLING SCORE PROGRAM...')
C       READ FIRST THREE PINS
      READ (2,10) P1
   10 FORMAT(I2)
      READ (2,10) P2
      READ (2,10) P3
      FN = 1
      GT = 0
C       TEST IF STRIKE IF P1 == 10
   20 IF (P1 - 10) 40, 30, 40
   30 FT = P1 + P2 + P3
      WRITE(3,31) FN, P1, P2, P3, FT
   31 FORMAT(' FRAME ', I2, '  *** STRIKE - PINS ', I3, I3, I3,
     C ' - FRAME SCORE ', I3)
      P1 = P2
      P2 = P3
C      DON'T READ IF LAST FRAME - NO MORE PINS
      IF (FN - 10) 35, 70, 35
   35 READ(2,10) P3
      GOTO 70
      
C       TEST IF SPARE IF P1 + P2 == 10 
   40 IF((P1 + P2) - 10) 60, 50, 60
   50 FT = P1 + P2 + P3
      WRITE(3,51) FN, P1, P2, P3, FT
   51 FORMAT(' FRAME ', I2, '  /// SPARE  - PINS ', I3, I3, I3,
     C ' - FRAME SCORE ', I3)
      P1 = P3
C       DON'T READ IF LAST FRAME NO MORE PINS
      IF (FN - 10) 55, 70, 55
   55 READ(2,10) P2
      READ(2,10) P3
      GOTO 70

C     REGULAR FRAME, JUST ADD THE PINS TOGETHER.
   60 FT = P1 + P2
      WRITE(3,61) FN, P1, P2, P3, FT
   61 FORMAT(' FRAME ', I2, '     REGULAR - PINS ', I3, I3, I3,
     C ' - FRAME SCORE ', I3)
      P1 = P3
C       DON'T READ IF LAST FRAME NO MORE PINS
      IF (FN - 10) 65, 70, 65
   65 READ(2,10) P2
      READ(2,10) P3

C  END OF LOOP, ACCUMULATE TOTAL, ADVANCE THE FRAME NUMBER.
   70 GT = GT + FT
      WRITE (3,75) GT
   75 FORMAT('     GAME TOTAL ', I3)
      FN = FN + 1

C     STOP AFTER FRAME 10 IS DONE.
      IF (FN - 11) 20, 80, 20
   80 WRITE(3,90)
   90 FORMAT(' GAME COMPLETE')


      CALL EXIT
      END
// XEQ
09
01
10
07
02
07
03
10
10
01
05
03
07
10
07
03
05
